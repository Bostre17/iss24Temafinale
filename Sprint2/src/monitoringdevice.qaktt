System monitoringdevice
 
/*
 * Sistema che funziona su RaspberryPi
 * Delega i comandi start/stop a sonardevice
 */
Event sonardata : distance(DISTANCE)   //emitted  by sonardevice or (better) by datacleaner
Event stateSonar : stateSonar(DISTANCE)
Event stateLed : stateLed(STATE)

Dispatch sonarstart : sonarstart(X)
Dispatch sonarwork : sonarwork(X)
Dispatch sonarstop  : sonarstop(X)

Dispatch doread : doread(X)

Context ctxmd ip [host= "localhost" port=8128]
Context ctxwis ip [host="127.0.0.1" port=8001]

ExternalQActor wis context ctxwis
  
QActor monitoringdevice context ctxmd{   
	[#	var DISTANCE=0
			var DLIMT= 0	
			var FULL= false	
		#]
	
	State s0 initial{  
		println("$name | ready") 
		//[# subscribeToLocalActor("sonar") #]
		delay 1000 //give time to create sonardevice
		//subscribeTo sonardevice for sonardata
		subscribeTo datacleaner for sonardata
		//println("$name subscribed to datacleaner") color magenta
		//delegate sonarstart to sonardevice
		delegate sonarstop  to sonardevice		
	}
	Transition t0 whenMsg	sonarstart -> initializing
	              whenMsg   sonarstop -> endwork
	              
	State initializing{
		onMsg( sonarstart : sonarstart(X) ){
			printCurrentMessage
			[# DLIMT=payloadArg(0).toInt() #]
			println("$name Starting: DLIMT=$DLIMT")
			
			forward sonardevice -m sonarstart : sonarstart(1)
		}
	}  
	Transition t0 whenEvent sonardata -> handlesonardata
	              whenMsg   sonarstop -> endwork
	              
	              
	State handlesonardata{
		printCurrentMessage color yellow
		onMsg( sonardata : distance(DISTANCE) ){
			
				[# DISTANCE = payloadArg(0).toInt() #]
				println("All'inizio il sonar misura $DISTANCE") color yellow
				
				if [# DISTANCE <= DLIMT #]{
					[#	FULL = true		#]
				println("$name | FULL: $DISTANCE") color green
				}
				else
				{
					[#	FULL = false	#]
				println("$name | EMPTY: $DISTANCE") color green
				}
				
				emit stateSonar : stateSonar($FULL)
		}
		
	}
	Transition t0 whenEvent sonardata -> handlesonardata
	              whenMsg   sonarstop -> endwork

	State endwork{
		println("$name | Stop working.") color magenta
		//forward sonar -m sonarstop : sonarstop(1)   
		forward sonardevice -m sonarstop : sonarstop(1)
	}
	Transition t0 whenMsg sonarstart -> initializing
}   



 
QActor datacleaner context ctxmd {
	[# 	var DISTANCE = 0; 
		var LAST = 0;
		var DIFF = 0;
		var TOLERANCE = 5#]
	State s0 initial {
		delay 1000
		subscribeTo sonardevice for sonardata		
		println("$name subscribed to sonardevice") color blue
	}
	Transition t0 whenEvent sonardata -> filter
	
	State filter {
		 println("$name distance=$DISTANCE") color black	
		onMsg(sonardata : distance(DISTANCE)) {
	      	[#  DISTANCE = payloadArg(0).toInt() #]	      	
	      	println("$name distance=$DISTANCE") color black
	      	if [# LAST != 0#] {
	      		[# DIFF = (DISTANCE - LAST) #]
	      	}
	      	else {
	      		[# DIFF = TOLERANCE #]
	      	}
	      	if [# DISTANCE < 300 && ( DIFF >= TOLERANCE || DIFF <= -TOLERANCE ) #]  {
	      		[# LAST = DISTANCE #]
	 			emitlocalstream  sonardata : distance($DISTANCE)
	     	}
     	}
	}	
	Transition t0 whenEvent sonardata -> filter
}
 

QActor sonardevice context ctxmd{
[# 
	lateinit var reader : java.io.BufferedReader
	var working = false
    lateinit var p : Process	
    var Distance = 0
#]	
	State s0 initial{
		println("$name | sonarstart") 
	}	
	Transition t0 
		whenMsg sonarstart -> startTheSonar
		whenMsg sonarstop  -> stopTheSonar
	
	State stopTheSonar{
     	[# working = false	#]	
	}
	Transition t0 
		whenMsg sonarstart -> startTheSonar
		whenMsg sonarstop  -> stopTheSonar
		whenMsg doread and [# ! working #] -> endOfRead  //potrebbe arrivare
	
	State startTheSonar{
	[#
    	working = true		
		p       = Runtime.getRuntime().exec("python sonar.py")
		reader  = java.io.BufferedReader(  java.io.InputStreamReader(p.getInputStream() ))	
	#]		
	}
	Goto readSonarData
	
	State readSonarData{
	[# 
		var data = reader.readLine()
		CommUtils.outyellow("$name with python: data = $data"   ) 
		if( data != null ){
		try{ 
			val vd = data.toFloat()
			val v  = vd.toInt()
			if( v <= 100 ){	//A first filter ...
				Distance = v				
			}else Distance = 0
		}catch(e: Exception){
				CommUtils.outred("$name readSonarDataERROR: $e "   )
		}
		}//if
		
	#]	
		if [# Distance > 0 #] { 
		    println("$name with python: data = $data"   ) color yellow
			emitlocalstream sonardata : distance($Distance)
			emit  sonardata : distance($Distance)
		}
		autodispatch doread : doread(1)
	}
	Transition t0 
	    whenMsg sonarstop  -> stopTheSonar
		whenMsg doread and [# working   #]   -> readSonarData 
		whenMsg doread and [# ! working #] -> endOfRead
		
	State endOfRead{
		[#
			
   	    p.destroy()
    	if (p.isAlive()) {
    	    p.destroyForcibly();
    	}
       	CommUtils.outred("$tt $name | endOfRead"  )
			
		#]
	}
	Transition t0 
		whenMsg sonarstart -> startTheSonar
		whenMsg sonarstop  -> stopTheSonar    //non dovrebbe arrivare, ma ...
	
}//sonardevice

QActor warningdevice context ctxmd{  
	// LED_STATE: 0 = OFF, 1 = ON, 2 = BLINK
	[#	var STATE = 0
		//var runtime = Runtime.getRuntime()
		#]
	
	State off initial{  
		println("$name | OFF")
		subscribeTo wis for stateLed
		//SCRIPT TURN OFF
	}
	Transition t0 whenEvent stateLed -> handleLedStateOne
	
		
	State on{  
		println("$name | ON") 
		//SCRIPT TURN ON
		        
	}
	Transition t0 whenEvent stateLed -> handleLedStateOne
		
	State blink{  
		println("$name | BLINK") 
		//SCRIPT TURN B
	}
	Transition t0 whenEvent stateLed -> handleLedStateOne
	
	
	State handleLedStateOne{
		printCurrentMessage color yellow
		onMsg( stateLed : stateLed(STATE) ){
			
				[# STATE = payloadArg(0).toInt() #]
				println("$name | New state: $STATE") color yellow	
		}
	}
	Goto blink if [# STATE == 2#] else conditionOnOff
	
	State conditionOnOff{
	}
	Goto on if [# STATE == 1 #] else off
}
/* 
QActor sonarusagemock context ctxmd{
	State s0 initial{
		println("$name |    START") color blue
		forward sonar24 -m sonarstart : sonarstart(1)
		delay 10000
		println("$name |    STOP") color blue
		forward sonar24 -m sonarstop : sonarstop(1)
		delay 2000
		println("$name |   RESTART") color blue
		forward sonar24 -m sonarstart : sonarstart(1)
//		delay 10000
//		println("$name |    RESTOP") color blue
//		forward sonar24 -m sonarstop : sonarstop(1)
//		delay 2000
//		println("$name | BYE") color blue
//		[# System.exit(0) #]
	}
}
*/